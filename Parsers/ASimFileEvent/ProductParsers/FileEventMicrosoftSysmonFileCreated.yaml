Parser:
  Title: Sysmon Event 11 - Create File
  Version: '0.2'
  LastUpdated: Aug 30, 2021
Product:
  Name: Windows Sysmon
Normalization:
  Schema: FileEvent
  Version: '0.1.0'
References:
- Title: ASIM File Schema
  Link: https://aka.ms/AzSentinelFileEventDoc
- Title: ASIM
  Link: https://aka.ms/AzSentinelNormalization
Description: ASIM Sysmon File Creation Event Parser (event number 11) from "Event" and "WindowsEvent" tables.
ParserName: vimFileEventMicrosoftSysmonCreated
ParserQuery: |
   // this is the parser for sysmon from Event table
      let Sysmon11_Event=(){
          Event
          | where Source == "Microsoft-Windows-Sysmon"
          | where EventID == 11
          | parse EventData with '<DataItem type="System.XmlData" time="'Time:datetime
                  '" sourceHealthServiceId="'sourceHealthServiceId
                  '"><EventData xmlns="http://schemas.microsoft.com/win/2004/08/events/event"><Data Name="RuleName">'RuleName:string
                  '</Data><Data Name="UtcTime">'UtcTime:datetime'</Data><Data Name="ProcessGuid">{'ProcessGuid:string
                  '}</Data><Data Name="ProcessId">'ProcessId:string
                  '</Data><Data Name="Image">'Image:string /// Image is the full path 
                  '</Data><Data Name="TargetFilename">'TargetFilename:string //// Full Path
                  '</Data><Data Name="CreationUtcTime">'CreationUtcTime:datetime  
                  '</Data></EventData></DataItem>'
          | extend
              EventType='FileCreated'
              , EventProduct='Sysmon'
              , EventSchemaVersion = '0.1.0'
              , EventResult='Success'
              , EventCount=int(1)
              , EventStartTime = TimeGenerated
              , EventEndTime = TimeGenerated
              , DvcOs='Windows'
              , TargetFileName_wo_Path=tostring(split(TargetFilename,'\\')[-1])
          | project-rename
              DvcHostname = Computer
              , ActingProcessName = Image
              , ActingProcessId = ProcessId
              , ActingProcessGuid = ProcessGuid
              , EventOriginalType=EventID
              , TargetFileCreationTime=CreationUtcTime
              , EventMessage=RenderedDescription
              , TargetFilePath=TargetFilename
              ,TargetFileName=TargetFileName_wo_Path
          | extend
                  Process = ActingProcessName
                  , Dvc = DvcHostname
                  , FilePath = TargetFilePath
                  };
      // this is the parser for sysmon from WindowsEvent table
      let Sysmon11_windowsEvent=(){
        WindowsEvent 
        | where Provider == "Microsoft-Windows-Sysmon"
        | where EventID == 11
        | extend 
                EventType = 'FileCreated',
                EventProduct = 'Sysmon',
                EventSchemaVersion = '0.1.0',
                EventResult='Success',
                EventCount=int(1),
                EventStartTime = TimeGenerated,
                EventEndTime = TimeGenerated,
                EventMessage=tostring(EventData.RenderedDescription),
                DvcOs='Windows',
                TargetFileName = tostring(split(EventData.TargetFilename,'\\')[-1]),
                TargetFileCreationTime=todatetime(EventData.CreationUtcTime),
                TargetFilePath=tostring(EventData.TargetFilename),
                ActingProcessName = tostring(EventData.Image),
                ActingProcessId = toint(EventData.ProcessId),
                ActingProcessGuid = tostring(EventData.ProcessGuid)
        | project-rename
                DvcHostname = Computer,
                EventOriginalType=EventID
        | extend 
                Process = ActingProcessName,
                Dvc = DvcHostname,
                FilePath = TargetFilePath
        };
      union isfuzzy=true
              Sysmon11_Event,
              Sysmon11_windowsEvent