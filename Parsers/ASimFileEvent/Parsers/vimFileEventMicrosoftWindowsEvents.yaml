Parser:
  Title: File Event ASIM filtering parser for Microsoft Windows Events
  Version: '0.1.3'
  LastUpdated: May 25, 2023
Product:
  Name: Microsoft Windows Events
Normalization:
  Schema: FileEvent
  Version: '0.1'
References:
- Title: ASIM File Event Schema
  Link: https://aka.ms/ASimFileEventDoc
- Title: ASIM
  Link: https://aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing Microsoft Windows Events (WindowsEvent and SecurityEvent tables) to the ASIM File Event normalized schema. Event IDs which are parsed as part of this parser: 4663, 5140
ParserName: vimFileEventMicrosoftWindowsEvents
ParserQuery: |
let Parser=()
{  
  let 5140AccessMaskLookup = datatable (AccessMask:string,EventType:string)
  [
      "0x1", "ReadData"
      , "0x2", "WriteData"
      , "0x4", "AppendData"
      , "0x8", "ReadEA"
      , "0x10", "WriteEA"
      , "0x20", "Execute/Traverse"
      , "0x40", "DeleteChild"
      , "0x80", "ReadAttributes"
      , "0x100", "WriteAttributes"
      , "0x10000", "Delete"
      , "0x20000", "Read_Control"
      , "0x40000", "Write_Dac"
      , "0x80000", "Write_Owner"
      , "0x100000", "Synchronize"
      , "0x1000000", "Access_Sys_Sec"
  ];
  let 5140AccessListLookup = datatable (AccessList:string,EventType:string)
  [
      "%%4416", "ListDirectory"
      , "%%4417", "AddFile"
      , "%%4418", "AddSubDirectory/CreatePipeInstance"
      , "%%4419", "N/A"
      , "%%4420", "N/A"
      , "%%4421", "N/A"
      , "%%4422", "N/A"
      , "%%4423", "N/A"
      , "%%4424", "N/A"
      , "%%1537", "N/A"
      , "%%1538", "N/A"
      , "%%1539", "N/A"
      , "%%1540", "N/A"
      , "%%1541", "N/A"
      , "%%1542", "N/A"
  ];
  let 4663AccessMaskLookup = datatable (AccessMask:string,EventType:string)
  [
      "0x1", "ObjectAccessed"
      , "0x10", "MetadataModified"
      , "0x100", "MetadataModified"
      , "0x10000", "ObjectDeleted"
      , "0x2", "ObjectModified"
      , "0x20000", "MetadataAccessed"
      , "0x4", "ObjectModified"
      , "0x40", "ObjectDeleted"
      , "0x40000", "MetadataModified"
      , "0x6", "ObjectModified"
      , "0x8", "MetadataAccessed"
      , "0x80", "MetadataAccessed"
      , "0x80000", "MetadataModified"
  ];
  let CommonUserTypeLookup = datatable (AccountType:string, ActorUserType:string)
  [
    'User', 'Regular',
    'Machine', 'Machine'
  ];    
  let CommonKnownSIDs = datatable (sid:string, username:string, type:string)
  [
    'S-1-5-18', 'Local System', 'Simple',
    'S-1-0-0', 'Nobody', 'Simple'
  ];
  let 4663events =
    union isfuzzy=false (
    WindowsEvent
        | where EventID == 4663 
            and EventData.ObjectType == "File"
            and EventData.ObjectName !startswith @"\Device\"
        | project TimeGenerated
            , EventID
            , AccessMask = tostring(EventData.AccessMask)
            , ProcessName = tostring(EventData.ProcessName)
            , SubjectUserSid = tostring(EventData.SubjectUserSid)
            , AccountType = tostring(EventData.AccountType)
            , Computer = tostring(EventData.Computer)
            , ObjectName = tostring(EventData.ObjectName)
            , ProcessId = tostring(EventData.ProcessId)
            , SubjectUserName = tostring(EventData.SubjectUserName)
            , SubjectAccount = tostring(EventData.SubjectAccount)
            , SubjectLogonId = tostring(EventData.SubjectLogonId)
            , HandleId = tostring(EventData.HandleId)
    )
  , ( 
    SecurityEvent
        | where EventID == 4663 
            and ObjectType == "File"
            and ObjectName !startswith @"\Device\"
        | project TimeGenerated
            , EventID
            , AccessMask
            , ProcessName
            , SubjectUserSid
            , AccountType
            , Computer
            , ObjectName
            , ProcessId
            , SubjectUserName
            , SubjectAccount
            , SubjectLogonId
            , HandleId
    )
        | lookup 4663AccessMaskLookup on AccessMask
        | extend ActingProcessName = ProcessName
            , ActingProcessId = tostring(toint(ProcessId))
            , TargetFilePath = ObjectName
        | project-away ProcessId
        | project-rename Process = ProcessName
            , FilePath = ObjectName
            , FileSessionId = HandleId
  ;
  let 5140events = 
    SecurityEvent
        | where EventID == 5140
            and ObjectType == "File"
        | project TimeGenerated, EventID, AccessMask, AccessList, SubjectUserSid, AccountType, Computer, SubjectUserName, SubjectAccount, SubjectLogonId, ShareName
        | lookup 5140AccessMaskLookup on AccessMask
        | lookup 5140AccessListLookup on AccessList
        | extend TargetFilePath = ShareName
        | project-rename FilePath = ShareName
    ;
  union isfuzzy=false
    4663events
    , 5140events
        | lookup CommonUserTypeLookup on AccountType
        | lookup CommonKnownSIDs on $left.SubjectUserSid == $right.sid
        | extend ActorUsername = iff (SubjectUserName == "-", username, SubjectAccount)
            , ActorUsernameType = iff(SubjectUserName == '-',type, 'Windows')
            , EventStartTime = TimeGenerated
            , EventEndTime = TimeGenerated
            , TargetFilePathFormat = "Windows Local"
            , EventOriginalType = tostring(EventID)
        | project-away EventID, AccountType, type, username
        | project-rename ActorUserId = SubjectUserSid
            , DvcHostname = Computer
            , ActorSessionId = SubjectLogonId
        | extend EventSchema = "FileEvent"
            , EventSchemaVersion = "0.1.1"
            , EventResult = "Success"
            , EventCount = int(1)
            , EventVendor = 'Microsoft'
            , EventProduct = 'Security Events'
            , Dvc = DvcHostname
            , ActorWindowsUsername = ActorUsername
            , User = ActorUsername
            , ActorUserSid = ActorUserId
};
Parser