Parser:
  Title: Network Session Event ASIM parser for Sysmon (Event 3)
  Version: '0.0.1'
  LastUpdated: Dec 13, 2022
Product:
  Name: Windows Sysmon
Normalization:
  Schema: NetworkSession
  Version: '0.2.4'
References:
- Title: ASIM Network Session Schema
  Link: https://aka.ms/ASimNetworkSessionDoc
- Title: ASIM
  Link: https:/aka.ms/AboutASIM
Description: |
  This ASIM parser supports normalizing Sysmon network session events (event 3) collected using the Event or WEF (WindowsEvent table) connectors to the ASIM Process Event normalized schema. 
ParserName: ASimNetworkSessionMicrosoftSysmon
EquivalentBuiltInParser: _ASim_NetworkSession_MicrosoftSysmon
ParserParams:
  - Name: disabled
    Type: bool
    Default: false
ParserQuery: | 
  let parser = (disabled:bool = false) {
  let Sysmon3_Event=(disabled:bool=false) {
        Event
        | where not(disabled)
        | where Source == "Microsoft-Windows-Sysmon" and EventID==3
        | parse-kv EventData as (
                    SourceIp:string,
                    DestinationIp:string,
                    SourceHostname:string,
                    DestinationHostname:string,
                    Initiated:bool,  // Initiated indicates the process initiated a connection (meaning outbound)
                    RuleName:string,
                    UtcTime:datetime,
                    ProcessGuid:string,
                    ProcessId:string,
                    Image:string,
                    User:string,
                    Protocol:string,
                    SourceIsIpv6:bool,
                    SourceIp:string,
                    SourcePort:int,
                    SourcePortName:string,
                    DestinationIsIpv6:bool,
                    DestinationIp:string,
                    DestinationPort:int,
                    DestinationPortName:string
                ) with (regex=@'<Data Name="(\w+)">{?([^>]*)}?</Data>')
        | project-away EventData
        | project-rename
                SrcHostname = SourceHostname,
                DstHostname = DestinationHostname
      };
  let Sysmon3_WindowsEvent=(disabled:bool=false){
        WindowsEvent
        | where not(disabled) 
        | where Provider == "Microsoft-Windows-Sysmon" and EventID == 3
        | extend
                SourceIp = tostring(EventData.SourceIp),
                DestinationIp = tostring(EventData.DestinationIp),
                DstHostname = tostring(EventData.DestinationHostname),
                SrcHostname = tostring(EventData.SrcHostname),
                Initiated = tobool(EventData.Initiated),
                RuleName = tostring(EventData.RuleName),
                UtcTime = todatetime(EventData.UtcTime),
                ProcessGuid = tostring(EventData.ProcessGuid),
                ProcessId = tostring(EventData.ProcessId),
                Image = tostring(EventData.Image),
                User = tostring(EventData.User),
                Protocol = tostring(EventData.Protocol),
                Initiated = tobool(EventData.Initiated), // Initiated indicates the process initiated a connection (meaning outbound)
                SourceIsIpv6 = tobool(EventData.SourceIsIpv6),
                SourceIp = tostring(EventData.SourceIp),
                SourcePort = toint(EventData.SourcePort),
                SourcePortName = tostring(EventData.SourcePortName),
                DestinationIsIpv6 = tobool(EventData.DestinationIsIpv6),
                DestinationIp = tostring(EventData.DestinationIp),
                DestinationPort = toint(EventData.DestinationPort),
                DestinationPortName = tostring(EventData.DestinationPortName)
        | project-away EventData
      };
  union Sysmon3_Event,Sysmon3_WindowsEvent
        | extend
                SrcUsernameType = 'Simple',
                SrcUsername = iff(not(Initiated), tostring(User), " "),
                SrcHostname = iff(not(Initiated), tostring(Computer), " "),
                SrcProcessId = iff(not(Initiated), tostring(ProcessId), " "),
                SrcProcessGuid = iff(not(Initiated), tostring(split(split(ProcessGuid, "{")[-1], "}")[0]), " "),
                SrcProcessName = iff(not(Initiated), tostring(Image), " "),
                DstUsernameType = 'Simple',
                DstUsername = iff(Initiated, tostring(User), " "),
                DstHostname = iff(Initiated, tostring(Computer), " "),
                DstProcessId = iff(Initiated, tostring(ProcessId), " "),
                DstProcessGuid = iff(Initiated, tostring(split(split(ProcessGuid, "{")[-1], "}")[0]), " "),
                DstProcessName = iff(Initiated, tostring(Image), " ")
        | project-away  ProcessId, ProcessGuid, Image,  *1
        | project-rename 
                EventStartTime = UtcTime,
                Dvc = Computer,
                IpAddr = SourceIp,
                Dst = DestinationIp,
                DstPortNumber = DestinationPort,
                SrcPortNumber = SourcePort,
                NetworkRuleName = RuleName
        | extend 
                EventEndTime = EventStartTime,
                DstHostname = Dvc,
                Hostname = Dvc,
                Src = IpAddr,
                DvcHostname = Dvc,
                DvcIpAddr = IpAddr,
                SrcIpAddr = IpAddr,
                DstIpAddr = Dst,
                EventType = 'EndpointNetworkSession',
                EventCount = int(1),
                EventVendor = 'Microsoft',
                EventSchemaVersion = '0.2.4',
                EventSchema = 'NetworkSession', 
                EventProduct = 'Sysmon for Windows',
                EventResult = 'Success',
                EventSeverity = 'Informational',
                DvcOs = 'Windows',
                Protocol = toupper(Protocol),
                EventOriginalType = '3', // Set with a constant value to avoid parsing
                SrcDomain = iff(tostring(split(Dvc, "\\")[0]) == Dvc, Dvc, tostring(split(Dvc, "\\")[0])),
                DvcDomainType = "Windows",
                SrcDomainType = "Windows",
                NetworkProtocolVersion = iff((DestinationIsIpv6) or (SourceIsIpv6), "IPV6", "IPV4"),
                NetworkProtocol = Protocol
        | project-away 
                AzureDeploymentID,
                Channel,
                Data,
                Destination*,
                Initiated,
                MG,
                ManagementGroupName,
                Message,
                ParameterXml,
                UserName,
                TenantId,
                Task,
                Protocol,
                Provider,
                RawEventData,
                RenderedDescription,
                Role,
                Source*
      };
      parser (disabled)
