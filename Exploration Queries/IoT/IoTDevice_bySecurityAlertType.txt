// Id: 5a2df97e-43f1-4454-857c-a9e2a8a0de7c
// DisplayName: IoT Devices With The Same Alert Type
// Description: IoT Devices With The Same Alert Type by count per device
// InputEntityType: SecurityAlert
// InputFields: [SecurityAlert_AlertType]
// OutputEntityTypes: [IoTDevice]
// DataConnector: #SecurityAlert
let IoTDevice_bySecurityAlertType = (v_SecurityAlert_AlertType:string){
    SecurityAlert 
    | where AlertType == v_SecurityAlert_AlertType
    | extend DymanicEntities = todynamic(Entities)
    | mv-expand DymanicEntities 
    | where DymanicEntities["Type"] == 	'iot-device'
    | project IoTDevice_DeviceId = tostring(DymanicEntities["DeviceId"]), IoTDevice_IoTHub_AzureResourceId = tostring(DymanicEntities["AzureResource"]["AzureResourceId"])
    | summarize occurance = count() by IoTDevice_DeviceId, IoTDevice_IoTHub_AzureResourceId
    | top 10 by occurance 
    };
IoTDevice_bySecurityAlertType('<AlertType>')