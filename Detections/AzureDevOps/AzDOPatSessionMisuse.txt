// Name: Azure DevOps Personal Access Token misuse
//
// Id: ac891683-53c3-4f86-86b4-c361708e2b2b
//
// Description: Alerts whenever a PAT is used in ways that PATs are not normally used.  May require whitelisting and baselining.
// Use this query for baselining:
// AzureDevOpsAuditing
// | distinct OperationName
//
// DataSource: #AzureDevOpsAuditing
//
// Severity: High
//
// QueryFrequency: 1h
//
// QueryPeriod: 3h
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// Tactics: #Execution #Impact
//
// Whitelisted UPNs should likely stay empty
let WhitelistedUpns = datatable(UPN:string)[
'foo@bar.com'
];
// Operation Name parts that will alert
let HasAnyBlacklist = datatable(OperationNamePart:string)[
'Security.',
'Project.',
'AuditLog.',
'Extension.',
];
// Distinct Operation Names that will flag
let HasExactBlacklist = datatable(OperationName:string)[
'Group.UpdateGroupMembership.Add',
'Library.ServiceConnectionExecuted',
'Pipelines.PipelineModified',
'Release.ReleasePipelineModified',
//'Git.RefUpdatePoliciesBypassed'
];
AzureDevOpsAuditing
| where AuthenticationMechanism startswith "PAT" and
  (
    OperationName has_any (HasAnyBlacklist) or
    OperationName in (HasExactBlacklist)
  )
  and ActorUPN !in (WhitelistedUpns)
| project TimeGenerated, ProjectName, ActorUPN, ActorDisplayName, IpAddress, UserAgent, OperationName, Details, Data