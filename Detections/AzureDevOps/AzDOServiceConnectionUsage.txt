// Name: Azure DevOps Service Conection Abuse
//
// Id: d564ff12-8f53-41b8-8649-44f76b37b99f
//
// Description: This detection flags builds/releases that use a large number of service connections if they aren't manually whitelisted.
// This is to determine if someone is hijacking a build/release and adding many service connections in order to abuse or dump credentials from service connections.
//
// DataSource: #AzureDevOpsAuditing
//
// Severity: High
//
// QueryFrequency: 2h
//
// QueryPeriod: 14d+
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// Tactics: #Execution #Impact #Escalation
//
// How many greater than Service Connections you want to view per build/release
let ServiceConnectionThreshold = 4;
let BypassDefIds = datatable(DefId:string, Type:string, ProjectName:string)
[
//"103", "Release", "ProjectA",
//"42", "Release", "ProjectB",
//"122", "Build", "ProjectB"
];
AzureDevOpsAuditing
| where TimeGenerated > ago(14d)
| where OperationName == "Library.ServiceConnectionExecuted" 
| extend DefId = tostring(Data.DefinitionId), Type = tostring(Data.PlanType), ConnectionId = tostring(Data.ConnectionId)
| parse ScopeDisplayName with OrganizationName ' (Organization)'
| summarize CurrentCount = dcount(tostring(ConnectionId)), ConnectionNames = make_set(tostring(Data.ConnectionName)) by OrganizationName, tostring(DefId), tostring(Type), ProjectId, ProjectName
| where CurrentCount > ServiceConnectionThreshold
| join kind=anti BypassDefIds on $left.DefId==$right.DefId and $left.Type == $right.Type and $left.ProjectName == $right.ProjectName
| extend link = iif(Type == "Build", strcat('https://dev.azure.com/', OrganizationName, '/', ProjectName, '/_build?definitionId=', DefId) ,strcat('https://dev.azure.com/', OrganizationName, '/', ProjectName, '/_release?_a=releases&view=mine&definitionId=', DefId))